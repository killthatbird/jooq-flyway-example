buildscript {
  ext {
    springBootVersion = '1.5.1.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath(
      "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
      'nu.studer:gradle-jooq-plugin:2.0.2',
      'org.flywaydb:flyway-gradle-plugin:4.1.1',
    )
  }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'

jar {
  baseName = 'jooq-flyway-example'
  version = '0.0.1-SNAPSHOT'
}

processResources {
  expand(project.properties)
}

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile(
    'org.flywaydb:flyway-core:4.1.1',
    'org.springframework.boot:spring-boot-starter-groovy-templates',
    'org.springframework.boot:spring-boot-starter-jooq',
    'org.springframework.boot:spring-boot-starter-web',
    'org.codehaus.groovy:groovy',
  )
  runtime(
    'org.springframework.boot:spring-boot-devtools',
    'org.postgresql:postgresql',
  )
  jooqRuntime(
    'org.postgresql:postgresql'
  )
  testCompile(
    'org.springframework.boot:spring-boot-starter-test',
  )
}

clean.doFirst {
  tasks.flywayClean.execute()
  delete 'src/main/generated'
}

flyway {
  url = db_url
  user = db_user
  password = db_password
}

jooq {
  example(sourceSets.main) {
    jdbc {
      driver = 'org.postgresql.Driver'
      url = db_url
      user = db_user
      password = db_password
    }
    generator {
      name = 'org.jooq.util.DefaultGenerator'
      strategy {
        name = 'org.jooq.util.DefaultGeneratorStrategy'
      }
      database {
        name = 'org.jooq.util.postgres.PostgresDatabase'
        inputSchema = 'public'
      }
      generate {
        relations = true
        deprecated = false
        records = true
        immutablePojos = true
        fluentSetters = true
      }
      target {
        packageName = 'gsch'
        directory = 'src/main/generated/java'
      }
    }
  }
}

generateExampleJooqSchemaSource.dependsOn flywayMigrate
